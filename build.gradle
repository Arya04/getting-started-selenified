apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'war'

group = 'getting-started'

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    compile 'org.springframework.boot:spring-boot-starter-web:1.5.6.RELEASE'
    testCompile 'com.coveros:selenified:2.0.0'
}


// Selenified configuration
// TODO most of this could be put in a selenified gradle plugin
sourceSets {
  test {
    java {
      srcDir 'src/test/java'
    }
  }
  seleniumTest {
    java {
      compileClasspath += main.output + test.output
      runtimeClasspath += main.output + test.output
      srcDir 'src/test/java'
    }
  }
}
configurations {
    seleniumTestCompile.extendsFrom testCompile
    seleniumTestRuntime.extendsFrom testRuntime
}
test {
    useTestNG()
}
task seleniumTest(type:Test) {
    outputs.upToDateWhen { false }
    classpath = project.sourceSets.seleniumTest.runtimeClasspath
    useTestNG() {
        systemProperties System.getProperties()
        if (project.hasProperty("suiteXmlFile")) {
            suites project.getProperty("suiteXmlFile")
        } else {
            suites "sample.xml" 
        }
        if (project.hasProperty("groups")) {
            includeGroups project.getProperty("groups")
        }
    }
}

